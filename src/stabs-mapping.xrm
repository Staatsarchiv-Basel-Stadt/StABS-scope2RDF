output r2rml

/*  Base URI templates
*   ==================
*   - UodURI: Unit of Description
*   - DescURI: Descriptors (Persons, Organisations, Places, Topics that are subjects of UoDs)
*   - queryURL: URL to the current public archival catalog
*/
template UodURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/"
template DescURI "http://ld.staatsarchiv.bs.ch/descriptor/id/{0}/"
template queryURL "https://query.staatsarchiv.bs.ch/query/detail.aspx?ID={0}"

/* Named Nodes (subclasses)
 * ========================
 */  
template AccessConditionsURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/MaterialExpression/AccessConditions/"
template ContentStructureURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/ContentStructure/"
template ContextURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/Context/"
template DatePointURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/Identity/DatePoint/"
template DateRangeURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/Identity/DateRange/"
template ExtentMediumURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/MaterialExpression/ExtentMedium/"
template IdentityURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/Identity/"
template MatExprURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/MaterialExpression/"
template MediaTypeURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/MaterialExpression/MediaType/"
template NotesURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/Notes/"
template StorageLocationURI "http://ld.staatsarchiv.bs.ch/uod/id/{0}/MaterialExpression/StorageLocation/"

/* Archival Description
 * ====================
 */
map archivalDescription from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
	subject template UodURI with ID_NR;	

	types
		xisadgr.ArchivalDescription

	properties
		xisadgr.hasArchivalDescription template UodURI with PARENT_ID_NR;
		xisadgr.hasContentStructure template ContentStructureURI with ID_NR;
		xisadgr.hasContext template ContextURI with ID_NR;
        xisadgr.hasIdentity template IdentityURI with ID_NR;
        xisadgr.hasMaterialExpression template MatExprURI with ID_NR;
		xisadgr.hasNotes template NotesURI with ID_NR;
}

/* Identity
 * --------
 */
map identity from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
	subject template IdentityURI with ID_NR;

	types
		xisadgr.Identity
		
	properties
		xisadgr.hasReferenceCode template queryURL with ID_NR;
		xisadgr.hasTitle from TITEL with language-tag ger;
		xisadgr.hasReferenceCodeOld from FRUEHERE_STABS_SIGNATUR;
		xisadgr.hasReferenceCodeSt from SIGNATUR;
		xisadgr.hasCreatedDatePoint from ENTSTEHUNGSZEITRAUM;
		xisadgr.hasCreatedDateRange template DateRangeURI with ID_NR;	
}

/* Context
 * -------
 */
map context from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
	subject template ContextURI with ID_NR;
	
	types
	    xisadgr.Context
	    
	properties
	    xisadgr.hasArchivalHistory from BESTANDSGESCHICHTE with language-tag ger;
	    xisadgr.hasArchivalHistory from VERWALTUNGSGESCHICHTE with language-tag ger;
	    xisadgr.hasArchivalHistory from VERWALTUNGSGESCHICHTE_BIOGRAFI with language-tag ger;
	    xisadgr.hasArchivalHistory from ARCHIVGESCHICHTE with language-tag ger;
	    xisadgr.hasArchivalHistory from ZEITRAUM_DER_MATERIALZUSAMMENS with language-tag ger;
//	    xisadgr.hasAcqInfo from;
        xisadgr.hasCreator from AKTENBILDNER_PROVENIENZ_TEXT with language-tag ger;
        xisadgr.hasCreator from VERFERTIGERIN with language-tag ger;
// @todo: Ablieferungen (Link); Zugänge         
}

/* Content Structure
 * -----------------
 */
map contentstructure from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
	subject template ContentStructureURI with ID_NR;
	
	types
	    xisadgr.ContentStructure
	    
	properties
	    xisadgr.hasScope from KOMMENTAR_ZUM_UMFANG with language-tag ger;
		xisadgr.hasContent from FORM_UND_INHALT with language-tag ger;
		xisadgr.hasAccruals from NEUZUGAENGE with language-tag ger;
// @add: Darin, Enthält, Inhalt, Bild Vorschau, Bewertung und Kassation, Ordnung und Klassifikation		
}

map daterange from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
	subject template DateRangeURI with ID_NR;
	
	types
	    xisadgr.DateRange
//	    
//	properties
//	    xisadgr.hasToDate from
//	    xisadgr.hasFromDate from
}


/* Material Expression: Base Map
 * =============================
 */
 map matexpr from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
 	subject template MatExprURI with ID_NR;
 	
 	types
 	    xisadgr.MaterialExpression
 	    
 	properties
 	    xisadgr.hasAccessConditions template AccessConditionsURI with ID_NR;
 	    xisadgr.hasExistenceLocation template StorageLocationURI with ID_NR;
 	    xisadgr.hasExistenceLocationOriginalsSt constant "Staatsarchiv Basel-Stadt (StABS)";
 	    xisadgr.hasExtentMediumSt from LAUFMETER;
 	    xisadgr.hasPhysTech from BESCHREIBSTOFF with language-tag ger;
 	    xisadgr.hasExtentMedium template ExtentMediumURI with ID_NR;
 }
 
 /* Access Conditions
  * -----------------
  */
 map accessconditions from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
 	subject template AccessConditionsURI with ID_NR;
 	
 	types
 	    xisadgr.AccessConditions
 	
// 	properties
// 	    xisadgr.hasAccessConditionsNotes
// 	    xisadgr.hasClassification
// 	    xisadgr.hasIntelectualPropertyRight
// 	    xisadgr.hasMetadataPrivacyProtection
// 	    xisadgr.hasOpenToThePublic
// 	    xisadgr.hasOtherAccessConditions
// 	    xisadgr.hasPrivacyProtection
// 	    xisadgr.hasRetentionPeriod
// 	    xisadgr.hasRetentionPeriodConditions
// 	    xisadgr.hasRetentionPeriodNotes

// @add Rechtsstatus
}
 
 /* Extent Medium
  * =============
  */
map extentmedium from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
	subject template ExtentMediumURI with ID_NR;
	
	types
	    xisadgr.ExtentMedium
	    
	properties
	    xisadgr.hasExtentMediumDataSize from LAUFMETER;
	    //xisadgr.hasExtentMediumDataUnit
	    xisadgr.hasExtentMediumDataQuantity from LAUFMETER;
	    xisadgr.hasExtentMediumDescription from LAUFMETER;
	    xisadgr.hasExtentMediumMedium from BESCHREIBSTOFF;
	    xisadgr.hasMediaType template MediaTypeURI with ID_NR;
}

/* Storage Location
 * ================
 * Attempt at describing the archives as an organisation
 */
map storagelocation from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
	subject template StorageLocationURI with ID_NR;
	
	types
	    xisadgr.StorageLocation
	    admin.Isil
	    schema.Organization
	    
	properties
	    admin.termCode constant "https://culture.ld.admin.ch/isil/CH-000027-1";
	    schema.identifier constant "CH-000027-1";
	    schema.email constant "stabs@bs.ch";
	    schema.sameAs constant "https://de.wikipedia.org/wiki/Staatsarchiv_Basel-Stadt";
	    schema.sameAs constant "https://www.wikidata.org/wiki/Q2324698";
	    schema.url constant "https://staatsarchiv.bs.ch";
} 
/* Media Type
 * ==========
 */
map mediatype from  WSD_STABS_SCP_ARCHIV_DBA.VE_STABS {
	subject template MediaTypeURI with ID_NR;
	
	types
	    xisadgr.MediaType
}
 
map notes from WSD_STABS_SCP_ARCHIV_DBA.VE_STABS { 
	subject template NotesURI with ID_NR;
	
	types 
	    xisadgr.Notes
	
	properties
	    xisadgr.hasNote from FORM_UND_INHALT with language-tag ger;
}
