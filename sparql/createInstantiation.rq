PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rico: <https://www.ica.org/standards/RiC/ontology#>
PREFIX stabs-d: <stabs:scope:data/>

WITH <https://ld.staatsarchiv.bs.ch/graph/source>

INSERT {  
  ?inst_uri a rico:Instantiation ;
    rico:title ?tit ;
    rico:identifier ?sig ;
    rico:physicalCharacteristics ?stabspC ;
    rico:hasCarrierType <https://ld.staatsarchiv.bs.ch/vocabularies/carrierTypes#undefined> ; # s. RDA Carrier Types (https://www.rdaregistry.info/termList/RDACarrierType/)
    rico:hasRepresentationType <https://ld.staatsarchiv.bs.ch/vocabularies/representationTypes#undefined> ; # s. RDA Media Types (https://www.rdaregistry.info/termList/RDAMediaType/)
    rico:isInstantiationOf ?uri 
    .
  ?uri rico:hasInstantiation ?inst_uri .
}

WHERE { 
  ?uri a rico:RecordResource ;
    stabs-d:ID_NR ?id .
  OPTIONAL { ?uri rico:title ?titel . }
  OPTIONAL { ?uri rico:identifier ?signatur . }
  OPTIONAL { ?uri stabs-d:PHYSISCHE_BESCHAFFENHEIT ?stabspC . }
  BIND (COALESCE(?titel, "[Titel fehlt]") AS ?tit)
  BIND (COALESCE(?signatur, "[Signatur fehlt]") AS ?sig)
  BIND (URI(CONCAT("https://ld.staatsarchiv.bs.ch/Instantiation/", ?id, "-i1")) as ?inst_uri)
  MINUS { ?s rico:isOrWasIncludedIn ?uri  .}
  }
